buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
    }
}

plugins {
    id "com.moowork.node" version "1.1.1"
}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'

dependencies {
    compile("org.springframework.boot:spring-boot-devtools")

    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

group = 'jonquarfoth'

jar {
    baseName = 'angular-boot-sample'
    version =  '1.0.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootRun {
    addResources = true
}

node {
    version = '7.4.0'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}/frontend/node_modules")
}

task testJs(type: NpmTask, dependsOn: 'npmInstall') {
    inputs.dir('frontend/src')

    args = ['run', 'test']
}
check.dependsOn('testJs')

task assembleJs(type: NpmTask, dependsOn: 'npmInstall') {
    inputs.dir('frontend/src')
    outputs.dir('src/main/resources/static/')
    args = ['run', 'build']
}
bootRun.dependsOn('assembleJs')
build.dependsOn('assembleJs')

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}